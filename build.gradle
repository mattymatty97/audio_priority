plugins {
    id 'fabric-loom' version '0.12.51'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

repositories {
    maven { url "https://maven.terraformersmc.com/" }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "com.terraformersmc:modmenu:latest.release"
    implementation 'com.google.code.gson:gson:latest.release'
}

loom {
    accessWidenerPath = file("src/main/resources/audio_priority.accesswidener")
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

def targetJavaVersion = 16
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release = targetJavaVersion
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    archivesBaseName = project.archives_base_name
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

def buildnumber = 1

tasks.register("updateBuildNumber") {
    doLast {
        Properties props = new Properties()

        File propsFile = new File("build-${project.minecraft_version}.properties")

        try {
            props.load(propsFile.newDataInputStream())
        }catch (IOException ex){
            props.setProperty('mod_buildnumber', "1")
            props.setProperty('mod_version', project.mod_version.toString())
        }

        String prevVersion = (props.getProperty('mod_version'))

        if (prevVersion.equals(project.mod_version)) {

            buildnumber = (((props.getProperty('mod_buildnumber')) as Integer) + 1)

        }

        props.setProperty('mod_buildnumber', buildnumber.toString())
        props.setProperty('mod_version', project.mod_version.toString())

        props.store(propsFile.newWriter(), null)

        props.load(propsFile.newDataInputStream())

    }
    outputs.upToDateWhen { false }
}

tasks.register("FinalizeBuild", Copy) {
    from "$buildDir/libs"
    into "output/libs"
    include "${project.archives_base_name}-${project.version}.jar"
    rename { String filename ->
        return "${project.archives_base_name}-${project.version}-${project.minecraft_version} build ${buildnumber}.jar"
    }
    doLast {
        mkdir "output/libs"
        println "Output File:"
        println "${project.archives_base_name}-${project.version}-${project.minecraft_version} build ${buildnumber}.jar"
    }
    outputs.upToDateWhen { false }
}

tasks.build.finalizedBy updateBuildNumber
tasks.updateBuildNumber.shouldRunAfter build
tasks.updateBuildNumber.finalizedBy FinalizeBuild